print(string: &String) {
    @print_size(string.pointer, string.length)
}

println(string: &String) {
    print(string)
    print("\n")
}

error(string: String) {
    @error_size(string, length(string))
}

errorln(string: String) {
    error(string)
    error("\n")
}

read(): String {
    let string = @allocate(128)
    @read_size(string, 128)
    return String(string, 128)
}

execute_command(file: String, arguments: Array<String>, wait: boolean) {
	let null_terminated_file = file + "\0"
	let arguments_new: Array<any> = Array<any>(arguments.length + 1)
	let i = 0
	while (i < arguments.length) {
		let thing = @cast_&String(arguments[i]) + "\0"
		arguments_new[i] = @cast_any(thing.pointer_consume())
		i = i + 1
	}

	@execute(null_terminated_file.pointer, arguments_new.pointer, wait)

	let i = 0
	while (i < arguments_new.length - 1) {
		let argument = arguments_new[i]
		@free(argument, length(argument) + 1)
		i = i + 1
	}

    print("")
}

length(string: &any): integer {
    let location = string
    let i = 0

    while (true) {
        if (@get_1(location) == 0) {
            return i
        }
        location = location + 1
        i = i + 1
    }

    return 999
}

_+(string1: &String, string2: &String): String {
    let string1_length = string1.length
    let string2_length = string2.length
    let new_memory = @allocate(string1_length + string2_length)
    @copy(string1.pointer, new_memory, string1_length)
    @copy(string2.pointer, new_memory + string1_length, string2_length)
    return String(new_memory, string1_length + string2_length)
}

integer_to_string(integer: integer): String {
    let string = ""
    if (integer == 0) {
        string = "0"
    }
    
    while (integer > 0) {
        let modulo = integer % 10
        integer = integer / 10
        let prepended_string = String(store(modulo + 48), 1)
        string = prepended_string +string
    }

    return string
}

boolean_to_string(boolean: boolean): String {
    if (boolean == 1) {
        return "true"
    }
    return "false"
}

store(value: integer): any {
    let new_memory = @allocate(1)
    @set_1(value, new_memory)
    return new_memory
}

not_equal(value1: &any, value2: &any): boolean {
    return @not(@equal(value1, value2))  
}

Array<A> {
    pointer: any
    length: integer

	<A>(size: integer): Array<A> {
	    return Array<A>(@allocate(size * 8), size)
	}

	free_custom<A>(array: &Array<A>) {
		if (A.memory_size() > 0) {
			let i = 0
			while (i < array.length) {
				if (@not(array[i] == 0)) {
					@free(array[i], A.memory_size())
				}
				i = i + 1
			}
		}
		@free(array.pointer, array.length * 8)
	}
}

_[]=<A>(array: &Array<A>, index: integer, value: A) {
    @set_8(@cast_integer(value), array.pointer + (index * 8))
}

_[]<A>(array: &Array<A>, index: integer): &A {
    return @cast_&A(@get_8(array.pointer + (index * 8)))
}

Function {
    pointer: any

	call(function: &Function, parameters: Array<any>): any {
	    return @call_function(function.pointer, parameters.pointer, parameters.length)
	}
}

String {
    pointer: any
    length: integer
    static: boolean

	(pointer: any): String {
	    return String(pointer, length(pointer), false)
	}
	
	(pointer: any, length: integer): String {
	    return String(pointer, length, false)
	}

	free_custom(string: &String) {
		if (@not(string.static)) {
			@free(string.pointer, string.length)
		}
	}
}

_==(string1: String, string2: String): boolean {
    if (string1.length != string2.length) {
        return false
    }

    let i = 0
    while (i < string1.length) {
        if (@get_1(string1.pointer + i) != @get_1(string2.pointer + i)) {
            return false
        }

        i = i + 1
    }

    return true
}

null(): any {
    return 0
}

LinkedList<A> {
    head: any

	<A>(): LinkedList<A> {
		return LinkedList<A>(LinkedList_Node<A>(null(), null()))
	}

	append<A>(linked_list: &LinkedList<A>, data: A) {
	    let node = @cast_&LinkedList_Node<A>(linked_list.head)
	    while (node.next != null()) {
	        node = @cast_&LinkedList_Node<A>(node.next)
	    }
	
	    let new_node = LinkedList_Node<A>(data, null())
	    node.next = new_node
	}

	insert<A>(linked_list: &LinkedList<A>, index: integer, data: A) {
	    let node = @cast_&LinkedList_Node<A>(linked_list.head)
	
	    let i = 0
	    while (i < index) {
	        node = @cast_&LinkedList_Node<A>(node.next)
	        i = i + 1
	    }
	
	    let temp_next = node.next
	    let new_node = LinkedList_Node<A>(data, @cast_any(temp_next))
	    node.next = new_node
	}

	remove<A>(linked_list: &LinkedList<A>, index: integer) {
	    let node = @cast_&LinkedList_Node<A>(linked_list.head)
	
	    let i = 0
	    while (i < index) {
	        node = @cast_&LinkedList_Node<A>(node.next)
	        i = i + 1
	    }
	
	    let temp_next_next = @cast_&LinkedList_Node<A>(node.next).next
		@cast_&LinkedList_Node<A>(node.next).free_custom()
		@free(node.next, LinkedList_Node.memory_size())
	    node.next = @cast_any(temp_next_next)
	}

	size<A>(linked_list: &LinkedList<A>): integer {
	    let node = @cast_&LinkedList_Node<A>(linked_list.head)
	    let i = 0
	
	    while (node.next != null()) {
	        node = @cast_&LinkedList_Node<A>(node.next)
	        i = i + 1
	    }
	
	    return i
	}

	free_custom<A>(list: &LinkedList<A>) {
		let node = list.head
		while (node != 0) {
			let new_node = @cast_&LinkedList_Node<A>(node).next
			@cast_&LinkedList_Node<A>(node).free_custom()
			@free(node, LinkedList_Node.memory_size())
			node = new_node
		}
	}
}

_[]<A>(linked_list: &LinkedList<A>, index: integer): &A {
    let node = @cast_&LinkedList_Node<A>(linked_list.head)
    let i = 0

    while (i < (index + 1)) {
        node = @cast_&LinkedList_Node<A>(node.next)
        i = i + 1
    }

    return @cast_&A(node.data)
}

LinkedList_Node<A> {
    data: any
    next: any

	free_custom<A>(node: &LinkedList_Node<A>) {
		if (@and(A.memory_size() != 0, node.data != 0)) {
			@free(node.data, A.memory_size())
		}
	}
}

Option<A> {
    data: any

	empty<A>(): Option<A> {
	    return Option<A>(null())
	}
	
	of<A>(data: A): Option<A> {
	    return Option<A>(data)
	}
	
	is_value<A>(option: &Option<A>): boolean {
	    return not_equal(option.data, null())
	}
	
	free_custom<A>(option: &Option<A>) {
		let test = option.data != null()
		let test2 = A.memory_size() > 0
		if (@and(test, test2)) {
			@free(option.data, A.memory_size())
		}
	}

	data<A>(option: &Option<A>): &A {
		return @cast_&A(option.data)
	}
}

_+(value1: &any, value2: &any): any {
    return @add(value1, value2)
}

_+(value1: integer, value2: integer): integer {
    return @cast_integer(@add(value1, value2))
}

_-(value1: any, value2: any): any {
    return @subtract(value1, value2)
}

_-(value1: integer, value2: integer): integer {
    return @cast_integer(@subtract(value1, value2))
}

_%(value1: integer, value2: integer): integer {
    return @cast_integer(@modulo(value1, value2))
}

_/(value1: integer, value2: integer): integer {
    return @cast_integer(@divide(value1, value2))
}

_*(value1: integer, value2: integer): integer {
    return @cast_integer(@multiply(value1, value2))
}

_<(value1: integer, value2: integer): boolean {
    return @less(value1, value2)
}

_>(value1: integer, value2: integer): boolean {
    return @greater(value1, value2)
}

_==(value1: &any, value2: &any): boolean {
    return @equal(value1, value2)
}

_!=(value1: &any, value2: &any): boolean {
    return not_equal(value1, value2)
}

integer.memory_size(): integer {
	return 0
}

boolean.memory_size(): integer {
	return 0
}

any.memory_size(): integer {
	return 0
}
