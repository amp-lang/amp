fn print(string: String) {
    print_size(string.pointer(), string.length());
};

fn println(string: String) {
    print(string);
    print("\n");
};

fn error(string: String) {
    error_size(string, length(string));
};

fn errorln(string: String) {
    error(string);
    error("\n");
};

fn read(): String {
    let string = allocate(128);
    read_size(string, 128);
    return String.new(string);
};

fn execute_command(file: String, arguments: Array, wait: boolean) {
    execute(to_null_terminated_string(file), to_null_terminated_array(arguments), wait);
    print("");
};

fn to_null_terminated_string(string: String): any {
    return concatenate(string, "\0").pointer();
};

fn to_null_terminated_array(array: Array): any {
    let new_array = Array.new(array.length());
    let i = 0;
    while (less(i, new_array.length())) {
        new_array.set(i, to_null_terminated_string(String(array.get(i))));
        i = i + 1;
    };
    return new_array.pointer();
};

fn length(string: any): integer {
    let location = string;
    let i = 0;

    while (true) {
        if (equal(get_1(location), 0)) {
            return i;
        };
        location = String(add(location, 1));
        i = i + 1;
    };

    return 999;
};

fn concatenate(string1: String, string2: String): String {
    let string1_length = string1.length();
    let string2_length = string2.length();
    let new_memory = allocate(string1_length + string2_length);
    copy(string1.pointer(), new_memory, string1_length);
    copy(string2.pointer(), new_memory + string1_length, string2_length);
    return String.new(new_memory, string1_length + string2_length);
};

fn integer_to_string(integer: integer): String {
    let string = "";
    if (equal(integer, 0)) {
        string = "0";
    };

    while (greater(integer, 0)) {
        let modulo = integer % 10;
        integer = integer / 10;
        string = concatenate(String.new(store(modulo + 48)), string);
    };
    return string;
};

fn boolean_to_string(boolean: boolean): String {
    if (equal(boolean, 1)) {
        return "true";
    };
    return "false";
};

fn store(value: integer): any {
    let new_memory = allocate(1);
    set_1(value, new_memory);
    return new_memory;
};

fn not_equal(value1: any, value2: any): boolean {
    return not(equal(value1, value2));  
};

struct Array {
    pointer: any;
    length: integer;
};

fn Array.new(size: integer): Array {
    return Array.new(allocate((1 + size) * 8), size);
};

fn Array.set(array: Array, index: integer, value: any) {
    set_8(integer(value), array.pointer() + (index * 8));
};

fn Array.get(array: Array, index: integer): any {
    return get_8(array.pointer() + (index * 8));
};

struct Function {
    pointer: any;
};

fn Function.call(function: Function, parameters: Array): any {
    return call_function(function.pointer(), parameters.pointer(), parameters.length());
};

struct String {
    pointer: any;
    length: integer;
};

fn String.new(pointer: any): String {
    return String.new(pointer, length(pointer));
};

fn String.equal(string1: String, string2: String) {
    if (not_equal(string1.length(), string2.length())) {
        return false;
    };

    let i = 0;
    while (less(i, string1.length())) {
        if (not_equal(get_1(string1.pointer() + i), get_1(string2.pointer() + i))) {
            return false;
        };

        i = i + 1;
    };

    return true;
};

fn null(): any {
    return 0;
};

struct LinkedList {
    head: LinkedList_Node;
};

fn LinkedList.new(): LinkedList {
    return LinkedList.new(LinkedList_Node.new(null(), LinkedList_Node(null())));
};

fn LinkedList.append(linked_list: LinkedList, data: any) {
    let node = linked_list.head();
    while (not_equal(node.next(), null())) {
        node = node.next();
    };

    let new_node = LinkedList_Node.new(data, LinkedList_Node(null()));
    node.next(new_node);
};

fn LinkedList.insert(linked_list: LinkedList, index: integer, data: any) {
    let node = linked_list.head();

    let i: integer = 0;
    while (less(i, index)) {
        node = node.next();
        i = i + 1;
    };

    let temp_next = node.next();
    let new_node = LinkedList_Node.new(data, temp_next);
    node.next(new_node);
};

fn LinkedList.remove(linked_list: LinkedList, index: integer) {
    let node = linked_list.head();

    let i: integer = 0;
    while (less(i, index)) {
        node = node.next();
        i = i + 1;
    };

    let temp_next_next = node.next().next();
    node.next(temp_next_next);
};

fn LinkedList.get(linked_list: LinkedList, index: integer): any {
    let node = linked_list.head();
    let i: integer = 0;

    while (less(i, index + 1)) {
        node = node.next();
        i = i + 1;
    };

    return node.data();
};

fn LinkedList.size(linked_list: LinkedList): integer {
    let node = linked_list.head();
    let i = 0;

    while (not_equal(node.next(), null())) {
        node = node.next();
        i = i + 1;
    };

    return i;
};

fn LinkedList.to_array(linked_list: LinkedList): Array {
    let size = linked_list.size();
    let array = Array.new(size);
    let i = 0;
    let node = linked_list.head().next();

    while (less(i, size)) {
        array.set(i, node.data());
        i = i + 1;
        node = node.next();
    };

    return array;
};

struct LinkedList_Node {
    data: any;
    next: LinkedList_Node;
};

struct Option {
    data: any;
};

fn Option.empty(): Option {
    return Option.new(null());
};

fn Option.of(data: any): Option {
    return Option.new(data);
};

fn Option.is_value(option: Option): boolean {
    return not_equal(option.data(), null());
};

fn add(value1: integer, value2: integer): integer {
    return integer(add(any(value1), any(value2)));
};